#lang racket
(require "interfaces.rkt")
(require rackunit)
(require "extras.rkt")
(require 2htdp/universe)   
(require 2htdp/image)

(provide
Throbber%:
make-throbber
 )


;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;; CONSTANTS

;; CANVAS
(define THROBBER-CHANGING-FACTOR 1)
(define CANVAS-WIDTH 500)
(define CANVAS-HEIGHT 600)
(define EMPTY-CANVAS (empty-scene CANVAS-WIDTH CANVAS-HEIGHT))


;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;; THROBBER
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

;; make-throbber: PosInt PosInt -> Throbber
;; GIVEN    : an x and a y position of a throbber
;; RETURNS  : an object representing a throbber at the given position.
;; EXAMPLES : see test cases
;; STRATEGY : combine simpler functions.
(define (make-throbber x y)
  (new Throbber%: [x x][y y]
       [throb-mouse-x-diff -1]
       [throb-mouse-y-diff -1]
       [r 5][selected? false][expanding? true]))


;; Constructor template for Throbber%:
;; A Throbber is a (new Throbber%: [x Integer][y Integer]
;;  [throb-mouse-x-diff Integer][throb-mouse-y-diff Integer] [r Integer]
;;  [selected? Boolean][expanding? Boolean])
;; Interpretation : An object of class Throbber%: represents
;;                  a thobber.

(define Throbber%:
  (class* object% (Toy<%>)
    
    (init-field x y)                ; x and y coordinate of Throbber
    
    (init-field throb-mouse-x-diff) ; difference between circle's x and mouse
                                    ; pointer's x coordinate.
    
    (init-field throb-mouse-y-diff) ; difference between circle's y and mouse
                                    ; pointer's y coordinate.
    
    (init-field r)                  ; the radius of the throbber.
    
    (init-field selected?)          ; the states whether the thobber is
                                    ; selected.
    
    (init-field expanding?)         ; the states whether the thobber is
                                    ; expanding.

    
    ; target throbber parameters
    (field [MIN-THROBBER-RADIUS 5])
    (field [MAX-THROBBER-RADIUS 20])
    
    ;; image for displaying the thobber when unselected
    (field [THOBBER-IMG-UNSELECTED (circle r "solid" "green")])
    
    ;; image for displaying the thobber when selected
    (field [THOBBER-IMG-SELECTED (circle r "outline" "green")])
    
     
    (super-new)
    
    ;; after-tick : -> Void
    ;; GIVEN    : No arguments
    ;; EFFECT   : Updates the throbber to the state,
    ;;            it should have following a tick.
    ;; EXAMPLE  : See test cases below
    ;; STRATEGY : Cases on selected? - whether the thobber is selected.
    (define/public (after-tick)
      (begin
        (println "in throbber after tick")
        (println r)
        (set! r (next-radius r))
        (println r)
        (set! expanding? (should-expand?))
        ))
    

    ;; next-radius: PosInt -> PosInt
    ;; GIVEN    : the radius of the thobber.
    ;; RETURNS  : Throbber's radius increased or decresed by
    ;;            THROBBER-CHANGING-FACTOR depending
    ;;            on whether its expanding.
    ;; EXAMPLES : see test cases below.
    ;; STRATEGY : Divide on cases whether toy is expanding or contracting.
    (define (next-radius r) 
      (if expanding?
          (+ r THROBBER-CHANGING-FACTOR)
          (- r THROBBER-CHANGING-FACTOR)))
    
    
    ;; should-expand? :-> Boolean
    ;; RETURN   : true if radius of throbber becomes less than 5.
    ;; EXAMPLES : see test cases below.
    ;; STRATEGY : Divide into case on radius - r.
    (define (should-expand?)
      (cond
        [(>= r MAX-THROBBER-RADIUS) false]
        [(<= r MIN-THROBBER-RADIUS) true]
        [else expanding?]))
    
    
    ;; to-scene : Scene -> Scene
    ;; GIVEN    : a scene to paint the throbber.
    ;; RETURNS  : a scene like the given one, but with
    ;;            this thobber painted on it.
    ;; STRATEGY : Cases on whether the throbber is selected.
    (define/public (add-to-scene scene)
       (if selected?
          (place-image (circle r "outline" "green")  x y scene)
          (place-image (circle r "solid" "green") x y scene)))
    
    
    ;; after-button-down : NonNegInt NonNegInt -> Void
    ;; GIVEN    : the location of a button-down event
    ;; EFFECT   : If the mouse pointer is inside throbber,
    ;;            make the throbber selected.
    ;; EXAMPLE  : THROBBER-AT-100-100 -> (new Throbber%: [x x][y y]
    ;;                                   [throb-mouse-x-diff 100]
    ;;                                   [throb-mouse-y-diff 100]
    ;;                                   [r 6][selected? true]
    ;;                                   [expanding? true]) where
    ;;                                   mx, my is 100,100 resp.
    ;; STRATEGY : Cases on whether the mouse is within the throbber.
    (define/public (after-button-down mx my)
      (if (inside-throbber? x y mx my r)
           (begin
             (set! throb-mouse-x-diff (- x mx)) 
             (set! throb-mouse-y-diff (- y my))
             (set! selected? true))
          this))
      
     
    
    ;; after-drag : NonNegInt NonNegInt -> Void
    ;; GIVEN    : the x and y coordinates of the mouse pointer.
    ;; EFFECT   : If throbber is selected, move it so that the vector from its position
    ;;            to the drag event is equal to the mx
    ;; EXAMPLE  : THROBBER-AT-100-100 -> (new Throbber%: [x x][y y]
    ;;                                   [throb-mouse-x-diff 101]
    ;;                                   [throb-mouse-y-diff 101]
    ;;                                   [r 6][selected? true]
    ;;                                   [expanding? true]) where
    ;;                                   mx, my is 101,101 resp.
    ;; STRATEGY : Divide into Cases on whether the throbber is selected.
    (define/public (after-drag mx my)
      (if selected?
          (begin
             (set! x (+ mx throb-mouse-x-diff))
             (set! y (+ my throb-mouse-y-diff)))
          this))
    
    
    ;; after-button-up : NonNegInt NonNegInt -> Void
    ;; GIVEN    : the x and y coordinates of mouse pointer.
    ;; EFFECT   : makes the throbber unselected
    ;; EXAMPLE  : THROBBER-AT-100-100 -> (new Throbber%: [x x][y y]
    ;;                                   [throb-mouse-x-diff 100]
    ;;                                   [throb-mouse-y-diff 100]
    ;;                                   [r 6][selected? false]
    ;;                                   [expanding? true]) where
    ;;                                   mx, my is 100,100 resp.
    ;; STRATEGY : combine simpler functions
    (define/public (after-button-up mx my)
      (begin
        (set! selected? false)))
        
    
    ;; inside-throbber? : Integer Integer Integer Integer -> Boolean
    ;; GIVEN    : x and y coordinates of two points respectively.
    ;; RETURNS  : true iff the given coordinate is inside the given throbber.
    ;; EXAMPLES : see tests below
    ;; STRATEGY : Combine simpler function
    (define (inside-throbber? x y mx my r)
      (<= (sqrt(+ (sqr (- x mx))  (sqr (- y my))))
          r))
    
    
    ;; after-key-event : KeyEvent -> Void
    ;; GIVEN    : A Key event
    ;; EFFECTS  : No effect
    ;; EXAMPLE  : THROBBER-AT-100-100 -> THROBBER-AT-100-100
    ;; DETAILS: a Throbber ignores key events
    (define/public (after-key-event kev) this)
    

    ;; after-drag : NonNegInt NonNegInt -> Throbber<%>
    ;; GIVEN    : the location of a move event
    ;; RETURNS  : a throbber like the given one, but after a move mouse event
    ;; EXAMPLE  : THROBBER-AT-100-100 -> THROBBER-AT-100-100
    ;; DETAILS: a Throbber ignores after-move
    (define/public (after-move  mx my) this)
    
    
    ;; -> Int
    ;; RETURNS: the x position of the center of the toy
    (define/public (toy-x)
      (exact-round x))
    
    ;; -> Int
    ;; RETURNS: the y position of the center of the toy
    (define/public (toy-y)
      (exact-round y))
    
    ;; -> Int
    ;; RETURNS: the radius of the thobber
    (define/public (toy-data) r)

    ;; For Test cases
    (define/public (for-test:explain-state)
      (list x y throb-mouse-x-diff throb-mouse-y-diff
            r selected? expanding?))
    
    ))


;;;;;;;;;;;;;;;;;;;;;;;;;;;;; TESTING FRAMEWORK - Throbber ;;;;;;;;;;;;;;;;;;;;;

(begin-for-test
  (local
    ((define INITIAL-THROBBER (make-throbber 100 100))
     (define EXPANDING-THROBBER (new Throbber%: [x 100][y 100]
                     [throb-mouse-x-diff -1]
                     [throb-mouse-y-diff -1]
                     [r 19][selected? false][expanding? false]))
     (define SHRINKING-THROBBER (new Throbber%: [x 300] [y 150]
                                     [throb-mouse-x-diff -1]
                                     [throb-mouse-y-diff -1]
                                     [r 7] [selected? false]
                                     [expanding? false]))
     (define BIGGER-THROBBER (new Throbber%: [x 300] [y 150]
                                     [throb-mouse-x-diff -1]
                                     [throb-mouse-y-diff -1]
                                     [r 21] [selected? false]
                                     [expanding? false]))
     (define SMALLER-THROBBER (new Throbber%: [x 300] [y 150]
                                     [throb-mouse-x-diff -1]
                                     [throb-mouse-y-diff -1]
                                     [r 4] [selected? false]
                                     [expanding? false]))
     )
    (check-equal?
     (send INITIAL-THROBBER toy-x)
     100)

    (check-equal?
     (send INITIAL-THROBBER toy-y)
     100)

   ;; calling after-tick
    (send INITIAL-THROBBER after-tick)
    (check-equal?
     (send INITIAL-THROBBER toy-data)
     6)

    (check-equal?
     (send INITIAL-THROBBER add-to-scene EMPTY-CANVAS)
     (place-image (circle 6 "solid" "green")
                  100 100 EMPTY-CANVAS))

    ;; calling after-tick function of expanding throbber
    (send EXPANDING-THROBBER after-tick)
    (check-equal?
     (send EXPANDING-THROBBER toy-data)
     18)

    (send BIGGER-THROBBER after-tick)
    (check-equal?
     (send BIGGER-THROBBER toy-data)
     20)

    (send SMALLER-THROBBER after-tick)
    (check-equal?
     (send SMALLER-THROBBER toy-data)
     3)

    ;; calling after-key for throbber
    (send INITIAL-THROBBER after-key-event "p")
    (check-equal? (send EXPANDING-THROBBER for-test:explain-state)
                  (list 100 100 -1 -1 18 #f #f))

    ;; calling button-down function
    (send INITIAL-THROBBER after-button-down 100 100)
    (check-equal? (send INITIAL-THROBBER for-test:explain-state)
                  (list 100 100 0 0 6 #t #t))

    ;; calling button-down for expanding throbber
    (send EXPANDING-THROBBER after-button-down 0 0)
    (check-equal? (send EXPANDING-THROBBER for-test:explain-state)
                  (list 100 100 -1 -1 18 #f #f))

    ;; calling button-up of throbber
    (send EXPANDING-THROBBER after-button-up 20 30)
    (check-equal? (send EXPANDING-THROBBER for-test:explain-state)
                  (list 100 100 -1 -1 18 #f #f))

    ;; calling after-move of throbber
    (send EXPANDING-THROBBER after-move 20 30)
    (check-equal? (send EXPANDING-THROBBER for-test:explain-state)
                  (list 100 100 -1 -1 18 #f #f))

    ;; calling after-tick function for shrinking throbber
    (send SHRINKING-THROBBER after-tick)
    (check-equal? (send EXPANDING-THROBBER for-test:explain-state)
                  (list 100 100 -1 -1 18 #f #f))

    ;; calling after-drag function for initial-throbber
    (send INITIAL-THROBBER after-drag 20 30)
    (check-equal? (send INITIAL-THROBBER for-test:explain-state)
                  (list 20 30 0 0 6 #t #t))

    (send EXPANDING-THROBBER after-drag 20 30)
    (check-equal? (send INITIAL-THROBBER for-test:explain-state)
                  (list 20 30 0 0 6 #t #t))

    ;; calling add-to-scene for throbber
    (check-equal?
     (send INITIAL-THROBBER add-to-scene EMPTY-CANVAS)
     (place-image (circle 6 "outline" "green")
                  20 30 EMPTY-CANVAS))

    
    ))
                                     